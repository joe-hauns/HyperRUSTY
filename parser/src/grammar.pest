WHITESPACE = _{ " " | "\t" | "\r\n" | "\n" }

path_formula = { ("Forall" | "Exists") ~ ident ~ (":" ~ ident)? ~ "." ~ form_rec }
form_rec = { path_formula | traj_formula | inner_hltl }
traj_formula = { ("A" | "E") ~ ident ~ "." ~ ahltl_form_rec }
ahltl_form_rec = { traj_formula | inner_altl }

inner_hltl = { hequal } 
inner_altl = { aequal }

hequal = { himpl ~ ("=" ~ hequal)? }
himpl = { hdisj ~ ("->" ~ himpl)? }
hdisj = { hconj ~ ("|" ~ hdisj)? }
hconj = { hweak ~ ("&" ~ hconj)? }
hweak = { huntl ~ ("W" ~ hweak)? }
huntl = { hrels ~ ("U" ~ huntl)? }
hrels = { hfactor ~ ("R" ~ hrels)? }
hfactor = { unop ~ hfactor | "(" ~ form_rec ~ ")" | hltl_atom }
hltl_atom = { ident ~ "[" ~ ident ~ "]" | constant | number }


aequal = { aimpl ~ ("=" ~ aequal)? }
aimpl = { adisj ~ ("->" ~ aimpl)? }
adisj = { aconj ~ ("|" ~ adisj)? }
aconj = { aweak ~ ("&" ~ aconj)? }
aweak = { auntl ~ ("W" ~ aweak)? }
auntl = { arels ~ ("U" ~ auntl)? }
arels = { afactor ~ ("R" ~ arels)? }
afactor = { aunop ~ afactor | "(" ~ inner_altl ~ ")" | altl_atom }
altl_atom = { ident ~ "[" ~ ident ~ "]" ~ "[" ~ ident ~ "]" | constant | number}

aunop = { "G" | "F" | "~" }
unop = { "G" | "F" | "X" | "~" }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | ".")* }
constant = {"TRUE" | "FALSE"}
number = @{ ASCII_DIGIT+ | "#b" ~ ("0" | "1")+ }

// Entry point for the parser
formula = _{ SOI ~ form_rec ~ EOI }
